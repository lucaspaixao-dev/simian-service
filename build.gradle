buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlint_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
    }
}

apply plugin: 'jacoco'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'io.gitlab.arturbosch.detekt'

group 'com.github.lucasschwenke'
version '1.0.0'
mainClassName = "io.ktor.server.netty.EngineMain"

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://kotlin.bintray.com/ktor' }
}

dependencies {
    //ktor
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"

    //koin
    compile "org.koin:koin-ktor:$koin_version"

    //db
    compile "org.mongodb:mongo-java-driver:$mongo_version"

    //utils
    compile "io.azam.ulidj:ulidj:$ulid_version"

    //tests
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_5_version"
    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_5_version"
    testCompile "io.mockk:mockk:$mockk_version"
    testCompile "org.assertj:assertj-core:$assertions_version"
    testCompile "de.bwaldvogel:mongo-java-server:$mongo_db_test_version"
    testCompile "org.skyscreamer:jsonassert:$json_assert_version"

    //linter
    detekt "io.gitlab.arturbosch.detekt:detekt-formatting:$detekt_version"
    detekt "io.gitlab.arturbosch.detekt:detekt-cli:$detekt_version"
}


detekt {
    failFast = true
    buildUponDefaultConfig = true
    config = files("default-detekt-config.yml")

    reports {
        html.enabled = true
        xml.enabled = true
        txt.enabled = true
    }
}

tasks.detekt.dependsOn ktlintCheck

ktlint {
    verbose = true
}

test.dependsOn ktlintCheck

def ignorePackagesInJacocoReport = { classDirectories ->
    files(classDirectories.files.collect {
        fileTree(dir: it,
                exclude: [
                        'com/github/lucasschwenke/simian/application/SimianApplication**',
                        'com/github/lucasschwenke/simian/application/config/**',
                        'com/github/lucasschwenke/simian/application/web/exceptions/**',
                        'com/github/lucasschwenke/simian/application/web/requests/**',
                        'com/github/lucasschwenke/simian/application/config/**',
                        'com/github/lucasschwenke/simian/common/**',
                        'com/github/lucasschwenke/simian/domain/exceptions/**'
                ])
    })
}

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }

    afterEvaluate {
        classDirectories = ignorePackagesInJacocoReport(classDirectories)
    }
}

jacocoTestReport {
    reports.xml.enabled = true

    afterEvaluate {
        classDirectories = ignorePackagesInJacocoReport(classDirectories)
    }
}

test.finalizedBy jacocoTestReport
test.finalizedBy jacocoTestCoverageVerification

test {
    useJUnitPlatform()

    filter {
        includeTestsMatching "com.github.lucasschwenke.simian.*"
    }

    testLogging {
        events "failed", "passed", "skipped"
        showExceptions = true
        exceptionFormat = "full"
        showCauses = true
    }
}

task componentTest(type: Test) {
    useJUnitPlatform()
    maxHeapSize "512m"
    failFast = true

    environment "MONGO_HOST", "TEST"
    environment "MONGO_AUTH_USER", "TEST"
    environment "MONGO_DATABASE", "TEST"
    environment "MONGO_AUTH_PASSWORD", 1234

    filter {
        includeTestsMatching "componentTests.*"
    }

    testLogging {
        events "failed", "passed", "skipped"
        showExceptions = true
        exceptionFormat = "full"
        showCauses = true
    }

    mustRunAfter test
}

test.finalizedBy componentTest
